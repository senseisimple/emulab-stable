#!/usr/bin/perl -wT

#
# EMULAB-COPYRIGHT
# Copyright (c) 2000-2007 University of Utah and the Flux Group.
# All rights reserved.
#

use English;
use Getopt::Std;
use XML::Simple;

#
# Change delay params for a link.
#
sub usage()
{
    print(STDERR
	  "Usage: delay_config [-m] [-d] [-s vnode] <pid> <eid> <link>".
	  " PARAM=VALUE ...\n".
	  "       delay_config [-d] -X <xmlfile>\n".
	  "Required: pid, eid, link, and at least one parameter to change!\n".
	  "  pid = Project ID\n".
	  "  eid = Experiment ID\n".
	  " link = link name from ns file, ie. 'link1' in\n".
	  "        'set link1 [\$ns duplex-link \$A \$B 10Kb 0ms DropTail]'\n".
	  "Options:\n".
	  "   -d = turn on debugging\n".
	  "   -s = Select the source of the link to determine which pipe\n".
	  "   -m = Modify the base experiment in addition to current state.\n".
	  "   -X = Get args and parameters from an XML file.\n".
	  "Parameters:\n".
	  " BANDWIDTH=NNN    - N=bandwidth (10-100000 Kbits per second)\n",
	  " PLR=NNN          - N=lossrate (0 <= plr < 1)\n".
	  " DELAY=NNN        - N=delay (one-way delay in milliseconds > 0)\n".
	  " LIMIT=NNN        - The queue size in bytes or packets\n".
	  " QUEUE-IN-BYTES=N - 0 means in packets, 1 means in bytes\n".
	  "RED/GRED Options: (only if link was specified as RED/GRED)\n".
	  " MAXTHRESH=NNN    - Maximum threshold for the average queue size\n".
	  " THRESH=NNN       - Minimum threshold for the average queue size\n".
	  " LINTERM=NNN      - Packet dropping probability\n".
	  " Q_WEIGHT=NNN     - For calculating the average queue size\n");
    # Web interface cares about this return value!
    exit(2);
}
my  $optlist = "dms:cX:";

#
# Configure variables
#
my $TB		= "@prefix@";
my $TBOPS       = "@TBOPSEMAIL@";
my $TEVC        = "$TB/bin/tevc";
my $GENTOPO     = "$TB/libexec/gentopofile";

# Locals
my $pid;
my $eid;
my $link;
my $modify     = 0;
my $srcvnode;
my %config;
my $pipeno;
my $pipe;
my $debug      = 0;
my $compatmode = 0;

#
# Testbed Support libraries
#
use lib "@prefix@/lib";
use libdb;
use libtestbed;

#
# Function prototypes
#
sub ChangeDelayConfig();
sub ChangeLinkDelayConfig();
sub ChangeVirtLans();
sub ParseXmlArgs($$$$$$);
sub fatal($);

#
# These are the fields that we allow to come in from the XMLfile.
#
my $SLOT_OPTIONAL	= 0x1;	# The field is not required.
my $SLOT_REQUIRED	= 0x2;  # The field is required and must be non-null.
my $SLOT_ADMINONLY	= 0x4;  # Only admins can set this field.
my $this_user;			# Used for $SLOT_ADMINONLY.
my %xmlfields =
    # XML Field Name        DB slot name         Flags             Default
    ("pid"		=> ["pid",		$SLOT_REQUIRED],
     "eid"		=> ["eid",		$SLOT_REQUIRED],
     "link"		=> ["vname",		$SLOT_REQUIRED],

     "vnode"		=> ["vnode",		$SLOT_OPTIONAL],
     "modify"		=> ["modbase",		$SLOT_OPTIONAL,	   0],
     "compat"		=> ["compat",		$SLOT_OPTIONAL,	   0],

     "bandwidth"	=> ["bandwidth",	$SLOT_OPTIONAL],
     "plr"		=> ["lossrate",		$SLOT_OPTIONAL],
     "delay"		=> ["delay",		$SLOT_OPTIONAL],
     "limit"		=> ["q_limit",		$SLOT_OPTIONAL],
     "queue-in-bytes"	=> ["q_bytes",		$SLOT_OPTIONAL],
     "thresh"		=> ["q_maxthresh",	$SLOT_OPTIONAL],
     "minthresh"	=> ["q_minthresh",	$SLOT_OPTIONAL],
     "linterm"		=> ["q_linterm",	$SLOT_OPTIONAL],
     "q_weight"		=> ["q_weight",		$SLOT_OPTIONAL]);

#
# Turn off line buffering on output
#
$| = 1;

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/sbin:/usr/bin:/usr/sbin";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Parse command arguments. Once we return from getopts, all that should be
# left are the required arguments.
#
%options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{"d"})) {
    $debug = 1;
}
if (defined($options{"c"})) {
    $compatmode = 1;
}
if (!defined($options{"X"})) {
    my $pid  = shift(@ARGV);
    my $eid  = shift(@ARGV);
    my $link = shift(@ARGV);
}
else {
    $xmlfile = $options{"X"};

    my %xmlargs = (); 
    my %errors = ();
    ParseXmlArgs($xmlfile, "virt_lans", \%xmlfields, $debug,
		 \%xmlargs, \%errors);
    if (keys(%errors)) {
	foreach my $errkey (keys(%errors)) {
	    my $errval = $errors{$errkey};
	    print "${errkey}: $errval\n";
	}
	fatal("XML arg error");
    }

    # There should be no other trailing args along with -X <xmlfile>.
    usage()
	if (@ARGV > 0);

    foreach my $arg (keys(%xmlargs)) {
	# Required.
	if ($arg eq "pid") {
	    $pid = $xmlargs{"pid"};
	}
	elsif ($arg eq "eid") {
	    $eid = $xmlargs{"eid"};
	}
	elsif ($arg eq "link") {
	    $link = $xmlargs{"link"};
	}
	# Optional.
	elsif ($arg eq "modify") {
	    $modify = $xmlargs{"modify"};
	}
	elsif ($arg eq "compat") {
	    $compatmode = $xmlargs{"compat"};
	}
	elsif ($arg eq "vnode") {
	    $srcvnode = $xmlargs{"vnode"};
	}
	# Push parameters onto ARGV for handling below.
	else {
	    push(@ARGV, "$arg=" . $xmlargs{$arg});
	}
    }
}
if (defined($options{"m"})) {
    $modify = 1;
}
if (defined($options{"s"})) {
    $srcvnode = $options{"s"};

    if ($srcvnode =~ /^([-\w]+)$/) {
	$srcvnode = $1;
    }
    else {
	die("*** Bad srcvnode name: $srcvnode.\n");
    }
}
if (@ARGV < 4 && !defined($options{"X"}) ) {
    usage();
}

#
# Untaint args.
#
if ($pid =~ /^([-\w]+)$/) {
    $pid = $1;
}
else {
    die("*** Bad data in pid: $pid.\n");
}
if ($eid =~ /^([-\w]+)$/) {
    $eid = $1;
}
else {
    die("*** Bad data in eid: $eid.\n");
}
if ($link =~ /^([-\w]+)$/) {
    $link = $1;
}
else {
    die("*** Bad data in link: $link.\n");
}

#
# Permission check.
#
if ($UID && !TBAdmin($UID) &&
    ! TBExptAccessCheck($UID, $pid, $eid, TB_EXPT_MODIFY)) {
    die("*** $0:\n".
	"    You do not have permission to modify the delay parameters!\n");
}

#
# No transitional experiments.
#
my $estate = ExpState($pid, $eid);
if (! $estate) {
    die("*** $0:\n".
	"    No such experiment $pid/$eid exists!\n");
}
if ($estate ne EXPTSTATE_ACTIVE &&
    $estate ne EXPTSTATE_SWAPPED) {
    die("*** $0:\n".
	"    Experiment $pid/$eid must be ACTIVE or SWAPPED\n".
	"    to alter its delay configuration.\n");
}

#
# Parse options, which will modify the existing configuration below.
#
while (@ARGV) {
    my $opt = shift(@ARGV);

    #
    # The parameter names correspond roughly to the names that the user
    # uses in the NS file. The $config{} keys correspond to the names of
    # the slots in the DB table. Well, roughly correspond since the delays
    # table uses q0_ and q1_, but we handle that below.
    #
    # XXX If you add to this list, be sure to add to agentmap hash below.
    #
    SWITCH: for ($opt) {
	/^BANDWIDTH=([0-9]*)$/i && do {
	    if ($1 > 100000 || $1 < 10) {
		usage();
	    }
	    $config{"bandwidth"} = $1;
	    last SWITCH;
	};
	/^DELAY=([0-9]*)$/i && do {
	    if ($1 < 0) {
		usage();
	    }
	    $config{"delay"} = $1;
	    last SWITCH;
	};
	/^PLR=([0-9\.]*)$/i && do {
	    if ($1 < 0 || $1 > 1) {
		usage();
	    }
	    $config{"lossrate"} = $1;
	    last SWITCH;
	};
	/^LIMIT=([0-9]*)$/i && do {
	    $config{"q_limit"} = $1;
	    last SWITCH;
	};
	/^QUEUE-IN-BYTES=(\d)$/i && do {
	    if ($1 != 0 && $1 != 1) {
		usage();
	    }
	    $config{"q_qinbytes"} = $1;
	    last SWITCH;
	};
	/^MAXTHRESH=(\d*)$/i && do {
	    $config{"q_maxthresh"} = $1;
	    last SWITCH;
	};
	/^THRESH=(\d*)$/i && do {
	    $config{"q_minthresh"} = $1;
	    last SWITCH;
	};
	/^LINTERM=([0-9\.]*)$/i && do {
	    $config{"q_linterm"} = $1;
	    last SWITCH;
	};
	/^Q_WEIGHT=([0-9\.]*)$/i && do {
	    $config{"q_weight"} = $1;
	    last SWITCH;
	};
	print "Invalid config option: $opt\n";
	usage();
    }
}

#
# More sanity checks.
#
foreach my $key (keys(%config)) {
    my $val = $config{$key};

    if ($debug) {
	print "$key=$val\n";
    }
}

#
# These map the names I am using in the config hash (which correspond
# to the DB slot names) into the event parameter names to send over to the
# delay agent.
#
my %agentmap =
    ( bandwidth		=> BANDWIDTH,
      delay             => DELAY,
      lossrate		=> PLR,
      q_limit		=> LIMIT,
      q_qinbytes	=> "QUEUE-IN-BYTES",
      q_maxthresh	=> MAXTHRESH,
      q_minthresh	=> THRESH,
      q_linterm		=> LINTERM,
      q_weight		=> Q_WEIGHT
    );

#
# Link or Lan!
# 
$query_result =
    DBQueryFatal("select member from virt_lans ".
		 "where pid='$pid' and eid='$eid' and vname='$link'");
if (!$query_result->numrows) {
    die("*** $0:\n".
	"    $link is not a link in $pid/$eid!\n");
}
my $islink = ($query_result->numrows == 2 ? 1 : 0);

#
# If experiment is not active, all we can do is change virt_lans.
#
if ($estate ne EXPTSTATE_ACTIVE) {
    if ($modify) {
	ChangeVirtLans();
    }
    else {
	print "Experiment $pid/$eid is not active. If you want to change\n".
	    "the base experiment configuration, please use the -m option!\n";
    }
    exit(0);
}

#
# Check to see if linkdelays or normal delay nodes. This is a gross
# complication of this entire script!
#
if (ChangeDelayConfig() < 0 &&
    ChangeLinkDelayConfig() < 0) {
    die("*** $0:\n".
	"    $link is not a traffic shaped link in $pid/$eid!\n");
}
if ($modify) {
    ChangeVirtLans();
}

#
# Inject an event. 
#
my $inject_string = "$TEVC -e $pid/$eid now $link";

# Direct the event to the whoever is handling this particular delay.
$inject_string .= "-${srcvnode} "
    if (defined($srcvnode));

$inject_string .= " modify ";

# Append the parameters.
foreach my $key (keys(%config)) {
    my $val = $config{$key};
    my $str = $agentmap{$key};

    #
    # When changing an entire lan, we have to adjust delay/loss since
    # there is a delay *to* the lan and a delay *from* the lan.
    # We do not do this for links since there is just a single delay.
    # When changing a single node in a lan, we fall back to the way things
    # operate in the parser, which is that the user intended to change both
    # directions to exactly this value. 
    #
    if (!$islink && !defined($srcvnode) && !$compatmode) {
        if ($key eq "delay") {
	    # This follows what is done in the parser.
	    $val = $val / 2.0;
	}
        elsif ($key eq "lossrate") {
	    # This follows what is done in the parser.
	    $val = 1-sqrt(1-$val);
	}
    }
    $inject_string .= "${str}=$val ";
}
if ($debug) {
    print "$inject_string\n";
}

system($inject_string) &&
    die("*** $0:\n".
	"    Failed to inject delay update event!\n");

print "*** WARNING:\n";
print "    You should *always* test your links and lans to make sure they\n";
print "    are behaving as you expect. You can do this by hand with ping,\n";
print "    or you can use Emulab's LinkTest feature.\n";
print "    Type 'linktest' into the Emulab search box to find out how to ".
    "use it.\n";
    
exit(0);

#
# Get current delay configuration. 
# 
sub ChangeDelayConfig() {
    my $query_clause;
    my $query_string;

    $query_string = "select * from delays ".
	"where pid='$pid' and eid='$eid' and vname='$link' and noshaping=0 ";

    $query_string .= "and (vnode0='$srcvnode' or vnode1='$srcvnode')"
	if (defined($srcvnode));

    #
    # Okay, see if there is a delay node.
    # 
    my $query_result = DBQueryFatal($query_string);
    if (! $query_result->numrows) {
	return -1;
    }
    
    if (defined($srcvnode) && $islink) {
	#
	# If given a source node of a duplex link, must map that into
	# the actual delay pipe side and the pipe number, since the
	# names of the slots in the DB table have a 0/1 appended. Big
	# Yuck.
	#
	if ($query_result->numrows != 1) {
	    die("*** $0:\n".
		"    Too many delay links for $link in $pid/$eid!\n");
	}
	my %row = $query_result->fetchhash();

	if ($row{'vnode0'} eq $srcvnode) {
	    $pipe   = 0;
	}
	else {
	    $pipe   = 1;
	}
	$pipeno = $row{"pipe${pipe}"};
	
	foreach my $key (keys(%config)) {
	    my $val = $config{$key};

	    $query_clause .= ", "
		if (defined($query_clause));
		
	    if ($key =~ /^q_(.*)/) {
		$query_clause .= "q${pipe}_${1}=$val";
	    }
	    else {
		$query_clause .= "${key}${pipe}=$val";
	    }
	}
    }
    else {
	#
	# We are changing a link or entire lan symmetrically, or we are
	# changing one node in a lan symmetrically. Note, we cannot yet
	# make an asymmetric change to an indvidual lan node. Sorry, too
	# much pain. 
	#
	foreach my $key (keys(%config)) {
	    my $val = $config{$key};

	    $query_clause .= ", "
		if (defined($query_clause));
		
	    if ($key =~ /^q_(.*)/) {
		$query_clause .= "q0_${1}=$val,q1_${1}=$val";
	    }
	    else {
		#
		# When changing an entire lan, we have to adjust delay/loss
		# since there is a delay *to* the lan and a delay *from*
		# the lan.  We do not do this for links since there is just
		# a single delay.  When changing a single node in a lan, we
		# fall back to the way things operate in the parser, which
		# is that the user intended to change both directions to
		# exactly this value.
		#
		if (!$islink && !defined($srcvnode) && !$compatmode) {
		    if ($key eq "delay") {
			# This follows what is done in the parser.
			$val = $val / 2.0;
		    }
		    elsif ($key eq "lossrate") {
			# This follows what is done in the parser.
			$val = 1-sqrt(1-$val);
		    }
		}
		$query_clause .= "${key}0=$val,${key}1=$val";
	    }
	}
    }
    
    #
    # Update the delays table.
    #
    $query_string =
	"update delays set $query_clause ".
	"where pid='$pid' and eid='$eid' and vname='$link' ";
    
    if (defined($srcvnode)) {
	$query_string .= "and (vnode0='$srcvnode' or vnode1='$srcvnode')";
    }
    if ($debug) {
	print "$query_string\n";
    }
    DBQueryFatal($query_string);
    return 0;
}    

#
# Change linkdelays.
# 
sub ChangeLinkDelayConfig() {
    my @query_clauses;
    my $query_string;

    $query_string = "select l.*,o.OS from linkdelays as l ".
	"left join nodes as n on n.node_id=l.node_id ".
	"left join os_info as o on o.osid=n.def_boot_osid ".
	"where l.pid='$pid' and l.eid='$eid' and l.vlan='$link' ";

    $query_string .= "and l.vnode='$srcvnode'"
	if (defined($srcvnode));

    #
    # Okay, see if there is a linkdelay.
    # 
    my $query_result = DBQueryFatal($query_string);
    if (! $query_result->numrows) {
	return -1;
    }

    #
    # XXX Check for linux; temporary.
    #
    while (my $row = $query_result->fetchrow_hashref()) {
	my $vnode = $row->{'vnode'};
	my $OS    = $row->{'OS'};

	if ($OS eq "Linux") {
	    print STDERR
		"*** $0:\n".
		"    Oops, dynamic events do not yet work on Linux!\n";
	    exit(2);
	}
    }

    #
    # This is much easier than delay nodes! As above, we cannot change a
    # lan node asymmetrically yet.
    #
    foreach my $key (keys(%config)) {
	my $val = $config{$key};

	if (!$islink && !defined($srcvnode) && !$compatmode) {
	    if ($key eq "delay") {
		# This follows what is done in the parser.
		$val = $val / 2.0;
	    }
	    elsif ($key eq "lossrate") {
		# This follows what is done in the parser.
		$val = 1-sqrt(1-$val);
	    }
	}
	push(@query_clauses, "${key}=$val");
	
	#
	# For a lan node, must also set the "r" params.
	# 
	if (!$islink &&
	    ($key eq "delay" ||
	     $key eq "bandwidth" ||
	     $key eq "lossrate")) {

	    push(@query_clauses, "r${key}=$val");
	}
    }
    
    #
    # Update the delays table.
    #
    $query_string = "update linkdelays set ".
	join(",", @query_clauses) . 
	" where pid='$pid' and eid='$eid' and vlan='$link' ";
    
    if (defined($srcvnode)) {
	$query_string .= "and vnode='$srcvnode'";
    }
    if ($debug) {
	print "$query_string\n";
    }
    DBQueryFatal($query_string);
    return 0;
}

#
# Change the virt_lans entry.
#
sub ChangeVirtLans() {
    my @query_clauses;
    my $query_string;
    my @query_clauses2;

    #
    # The first case is for changing a node in a duplex link asymmetrically.
    #
    if ($islink && defined($srcvnode)) {
	#
	# In a duplex link the delay/bw/plr params are split between the
	# two virt_lan members using the "r" params to hold the "from
	# switch" half of the value. This makes it rather confusing.
	# 
	foreach my $key (keys(%config)) {
	    my $val = $config{$key};

	    if ($key eq "delay") {
		my $delay = $val / 2.0;

		push(@query_clauses,  "delay=$delay");
		push(@query_clauses2, "rdelay=$delay");
	    }
	    elsif ($key eq "lossrate") {
		my $lossrate = 1-sqrt(1-$val);

		push(@query_clauses, "lossrate=$lossrate");
		push(@query_clauses2, "rlossrate=$lossrate");
	    }
	    elsif ($key eq "bandwidth") {
		push(@query_clauses, "bandwidth=$val");
		push(@query_clauses2, "rbandwidth=$val");
	    }
	    else {
		push(@query_clauses, "$key=$val");
	    }
	}
	$query_string = "update virt_lans set ".
	    join(",", @query_clauses) .
	    " where pid='$pid' and eid='$eid' and ".
	    "      vname='$link' and member like '${srcvnode}:%'";
	
	if ($debug) {
	    print "$query_string\n";
	}
	DBQueryFatal($query_string);

	if (@query_clauses2) {
	    $query_string =
		"update virt_lans set ".
		join(",", @query_clauses2) .
		" where pid='$pid' and eid='$eid' and ".
		"      vname='$link' and member not like '${srcvnode}:%'";
	
	    if ($debug) {
		print "$query_string\n";
	    }
	    DBQueryFatal($query_string);
	}
    }
    else {
	#
	# When changing an entire link or lan symmetrically its easy; they can
	# be done the same. When changing one node in a lan (symmetrically),
	# the numbers are slightly different for delay/lossrate, but otherwise
	# its the same operation, except for operating on a single node.
	#
	foreach my $key (keys(%config)) {
	    my $val = $config{$key};

	    if ($key eq "delay") {
		my $delay = $val;

		# This follows what is done in the parser;
		# See tb-set-node-lan-delay
		$delay = $delay / 2.0
		    if (!defined($srcvnode));

		push(@query_clauses, "delay=$delay");
		push(@query_clauses, "rdelay=$delay");
	    }
	    elsif ($key eq "lossrate") {
		my $lossrate = $val;

		# This follows what is done in the parser.
		# See tb-set-node-lan-lossrate
		$lossrate = 1-sqrt(1-$lossrate)
		    if (!defined($srcvnode));

		push(@query_clauses, "lossrate=$lossrate");
		push(@query_clauses, "rlossrate=$lossrate");
	    }
	    elsif ($key eq "bandwidth") {
		push(@query_clauses, "bandwidth=$val");
		push(@query_clauses, "rbandwidth=$val");
	    }
	    else {
		push(@query_clauses, "$key=$val");
	    }
	}

	$query_string = "update virt_lans set ".
	    join(",", @query_clauses) .
	    " where pid='$pid' and eid='$eid' and vname='$link'";

	#
	# A lan node change since it cannot be a link if srcvnode defined.
	# 
	if (defined($srcvnode)) {
	    $query_string .= " and member like '${srcvnode}:%'";
	}

	if ($debug) {
	    print "$query_string\n";
	}
	DBQueryFatal($query_string);
    }
    #
    # Now we need to regen the linktest map file ...
    #
    system("$GENTOPO $pid $eid");
    if ($?) {
	die("*** $0:\n".
	    "    $GENTOPO failed!\n");
    }
}

sub ParseXmlArgs($$$$$$) {
    my ($xmlfile, $table_name, $fields_ref, $debug, 
	$args_ref, $errs_ref) = @_;
    #
    # Input args:
    #  $xmlfile	   - XML file path.
    #  $table_name - table_regex table_name for low-level checking patterns.
    #  $fields_ref - xmlfields specification (hash reference.)
    #  $debug
    #
    # Output args:
    #  $args_ref   - Parsed argument values (hash reference.)
    #  $errs_ref   - Error messages on failure (hash reference.)

    #
    # Must wrap the parser in eval since it exits on error.
    #
    my $xmlparse = eval { XMLin($xmlfile,
				VarAttr => 'name',
				ContentKey => '-content',
				SuppressEmpty => undef); };
    if ($@) {
	$errs_ref->{"XML Parse Error"} = "Return code $@";
	return;
    }

    #
    # Make sure all the required arguments were provided.
    #
    my $key;
    foreach $key (keys(%{ $fields_ref })) {
	my (undef, $required, undef) = @{$fields_ref->{$key}};

	$errs_ref->{$key} = "Required value not provided"
	    if ($required & $SLOT_REQUIRED  &&
		! exists($xmlparse->{'attribute'}->{"$key"}));
    }
    return
	if (keys(%{ $errs_ref }));

    foreach $key (keys(%{ $xmlparse->{'attribute'} })) {
	my $value = $xmlparse->{'attribute'}->{"$key"}->{'value'};

	if ($debug) {
	    print STDERR "User attribute: '$key' -> '$value'\n";
	}

	$errs_ref->{$key} = "Unknown attribute"
	    if (!exists($fields_ref->{$key}));

	my ($dbslot, $required, $default) = @{$fields_ref->{$key}};

	if ($required & $SLOT_REQUIRED) {
	    # A slot that must be provided, so do not allow a null value.
	    if (!defined($value)) {
		$errs_ref->{$key} = "Must provide a non-null value";
		next;
	    }
	}
	if ($required & $SLOT_OPTIONAL) {
	    # Optional slot. If value is null skip it. Might not be the correct
	    # thing to do all the time?
	    if (!defined($value)) {
		next
		    if (!defined($default));
		$value = $default;
	    }
	}
	if ($required & $SLOT_ADMINONLY) {
	    # Admin implies optional, but thats probably not correct approach.
	    $errs_ref->{$key} = "Administrators only"
		if (! $this_user->IsAdmin());
	}

	# Now check that the value is legal.
	if (! TBcheck_dbslot($value, $table_name, $dbslot, 
			     TBDB_CHECKDBSLOT_ERROR)) {
	    $errs_ref->{$key} = TBFieldErrorString();
	    next;
	}

	$args_ref->{$key} = $value;
    }
}

sub fatal($) {
    my($mesg) = $_[0];

    die("*** $0:\n".
	"    $mesg\n");
}
