#!/usr/bin/perl -wT
#
# EMULAB-COPYRIGHT
# Copyright (c) 2005, 2006 University of Utah and the Flux Group.
# All rights reserved.
#
use English;
use Getopt::Std;
use Fcntl ':flock';

#
# Add a chat room to the jabber DB on ops. 
#
sub usage()
{
    print STDOUT "Usage: addjabberchat [-g <gid>] <pid>\n";
    exit(-1);
}
my $optlist = "dg:";
my $debug   = 0;
my $pid;
my $gid;

#
# Configure variables
#
my $TB		= "@prefix@";
my $TBOPS       = "@TBOPSEMAIL@";
my $CONTROL     = "@USERNODE@";
my $BOSSNODE	= "@BOSSNODE@";
my $CHATSUPPORT = @CHATSUPPORT@;
my $JABBERCHAT  = "$TB/sbin/chatsup";

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/usr/bin";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Turn off line buffering on output
#
$| = 1;

#
# Load the Testbed support stuff. 
#
use lib "@prefix@/lib";
use libdb;
use libtestbed;

#
# We don't want to run this script unless its the real version.
#
if ($EUID != 0) {
    die("*** $0:\n".
	"    Must be setuid! Maybe its a development version?\n");
}

#
# This script is setuid, so please do not run it as root. Hard to track
# what has happened.
# 
if ($UID == 0) {
    die("*** $0:\n".
	"    Please do not run this as root! Its already setuid!\n");
}

#
# If no chat support, just exit. 
#
if (! $CHATSUPPORT) {
    print "CHAT support is not enabled. Exit ...\n";
    exit(0);
}

#
# Parse command arguments. Once we return from getopts, all that should be
# left are the required arguments.
#
%options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{"d"})) {
    $debug = 1;
}
if (defined($options{"g"})) {
    $gid = $options{"g"};
    
    #
    # Untaint args.
    #
    if ($gid =~ /^([-\w]+)$/) {
	$gid = $1;
    }
    else {
	die("Bad data in gid: $gid");
    }
}
if (@ARGV != 1) {
    usage();
}
$pid = $ARGV[0];

#
# Untaint args.
#
if ($pid =~ /^([-\w]+)$/) {
    $pid = $1;
}
else {
    die("Bad data in pid: $pid.");
}
$gid = $pid
    if (!defined($gid));

my $chatroom = (($pid eq $gid) ? "$pid" : "$pid/$gid");

#
# This script always does the right thing, so no permission checks.
# In fact, all it does it call over to ops to run a script over there.
# Note that adduser will just update the password if the user already
# exist in the wiki. 
#

#
# We need the leader and the password from the DB. 
#
my $query_result =
    DBQueryFatal("select leader,mailman_password ".
		 "from groups where pid='$pid' and gid='$gid'");

if (!$query_result->numrows) {
    fatal("No such project/group $pid/$gid in the DB!");
}
my ($leader,$password) = $query_result->fetchrow_array();

if (!defined($password)) {
    $password = substr(TBGenSecretKey(), 0, 10);
    # Must untaint;
    if ($password =~ /^([-\w]+)$/) {
	$password = $1;
    }
    else {
	die("Bad data in password: $password.");
    }
    DBQueryFatal("update groups set mailman_password='$password' ".
		 "where pid='$pid' and gid='$gid'");
}

#
# For ssh.
#
$UID = $EUID;

if ($CONTROL ne $BOSSNODE) {
    my $optarg = ($debug ? "-d" : "");
	
    print "Adding chatroom $chatroom to the jabber DB on $CONTROL.\n";

    # Must serialize all jabber stuff. Be sure to use the same token!
    TBScriptLock("jabber_update") == 0 or
	fatal("Could not get the lock!");

    system("$JABBERCHAT $chatroom create $password $leader");
    
    my $status = $?;
    TBScriptUnlock();
    $? = $status;

    if ($?) {
	fatal("$JABBERCHAT failed!");
    }
}
exit(0);

sub fatal($)
{
    my($mesg) = $_[0];

    die("*** $0:\n".
	"    $mesg\n");
}
