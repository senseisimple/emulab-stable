#!/usr/bin/perl -wT
#
# EMULAB-COPYRIGHT
# Copyright (c) 2005, 2007 University of Utah and the Flux Group.
# All rights reserved.
#
use English;
use Getopt::Std;

#
# Change user details (fullname, password, email).
#
sub usage()
{
    print STDOUT "Usage: mmmodifymember <uid>\n";
    exit(-1);
}
my $optlist = "d";
my $debug   = 0;
my $dbuid;
my $target_uid;

#
# Configure variables
#
my $TB		= "@prefix@";
my $TBOPS       = "@TBOPSEMAIL@";
my $TBAUDIT	= "@TBAUDITEMAIL@";
my $CONTROL     = "@USERNODE@";
my $BOSSNODE	= "@BOSSNODE@";
my $OURDOMAIN   = "@OURDOMAIN@";
my $MAILMANSUPPORT= @MAILMANSUPPORT@;
my $SSH         = "$TB/bin/sshtb";
my $MMPROXY     = "$TB/sbin/mailmanproxy";

# Protos
sub fatal($);

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/usr/bin";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Turn off line buffering on output
#
$| = 1;

#
# Load the Testbed support stuff. 
#
use lib "@prefix@/lib";
use libdb;
use libtestbed;
use User;

#
# We don't want to run this script unless its the real version.
#
if ($EUID != 0) {
    die("*** $0:\n".
	"    Must be setuid! Maybe its a development version?\n");
}

#
# This script is setuid, so please do not run it as root. Hard to track
# what has happened.
# 
if ($UID == 0) {
    die("*** $0:\n".
	"    Please do not run this as root! Its already setuid!\n");
}

#
# If no mailman support, just exit. 
#
if (! $MAILMANSUPPORT) {
    print "MailMan support is not enabled. Exit ...\n";
    exit(0);
}

#
# Parse command arguments. Once we return from getopts, all that should be
# left are the required arguments.
#
%options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{"d"})) {
    $debug = 1;
}
usage()
    if (@ARGV != 1);

$target_uid = $ARGV[0];

#
# Untaint args.
#
if ($target_uid =~ /^([-\w]+)$/) {
    $target_uid= $1;
}
else {
    die("Bad data in uid: $target_uid");
}

my $target_user = User->Lookup($target_uid);
if (! defined($target_user)) {
    fatal("$target_uid does not exist!");
}
my $email    = $target_user->email();
my $password = $target_user->mailman_password();
my $fullname = $target_user->name();

#
# Note that since we are sending cleartext passwords over, pipe the info
# into its STDIN so that the passwords are not visible in a ps listing.
#
# For ssh.
#
$UID = $EUID;

if ($CONTROL ne $BOSSNODE) {
    my $optarg = ($debug ? "-d" : "");

    print "Resetting details for user $target_uid on $CONTROL.\n";

    # Must serialize some of the mailman stuff. Be sure to use the same token!
    TBScriptLock("mailman_update") == 0 or
	fatal("Could not get the lock!");

    # Watch for embedded quotes.
    $fullname =~ s/(\')/\'\\'\'/g;

    system("echo \"$password \'$fullname\'\" | ".
           "  $SSH -host $CONTROL $MMPROXY ".
	   "  $optarg modifymember $target_uid $email");

    my $status = $?;
    TBScriptUnlock();
    $? = $status;

    if ($?) {
	fatal("$MMPROXY failed on $CONTROL!");
    }
    
}
exit(0);

sub fatal($)
{
    my($mesg) = $_[0];

    die("*** $0:\n".
	"    $mesg\n");
}
