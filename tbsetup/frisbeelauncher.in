#!/usr/bin/perl -wT
#
# EMULAB-COPYRIGHT
# Copyright (c) 2000-2002, 2004, 2005, 2006 University of Utah and the Flux Group.
# All rights reserved.
#
use Getopt::Std;
use POSIX 'setsid';	 # For &daemonize
use POSIX ":sys_wait_h"; # For &WNOHANG
use Sys::Syslog;
use English;
use Socket;
use Errno qw(EADDRINUSE);

#
# This also kills a running frisbee.
#
sub usage()
{
    print "Usage: $0 [-d] [-k] <imageid>\n";
    print "-k:	Kill running frisbee.\n";
    print "-d:	Print debugging output.\n";
    exit(1);
}
my $optlist  = "dk";
my $debug    = 0;
my $killmode = 0;

# Configure variables
my $TB		= "@prefix@";
my $TBOPS	= "@TBOPSEMAIL@";
my $BOSSNODE_IP = "@BOSSNODE_IP@";
my $BASEADDR    = "@FRISEBEEMCASTADDR@";
my $BASEPORT    = "@FRISEBEEMCASTPORT@";
my $ELABINELAB  = @ELABINELAB@;

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/usr/bin";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Turn off line buffering on output
#
$| = 1;

use lib "@prefix@/lib";
use libdb;
use libtestbed;
use libtblog;

# Defines
my $FRISBEED	= "$TB/sbin/frisbeed";
my $LOGFILE	= "$TB/log/frisbeelauncher";
my $FRISBEEIMAGE= "$TB/sbin/frisbeeimage";

my $STD_BW	= 72000000; 	# 71.6Mb/sec w/1000HZ kernel
my $USR_BW	= 54000000;	# 53.7Mb/sec w/1000HZ kernel

#
# Parse command arguments. Once we return from getopts, all that should be
# left are the required arguments.
#
%options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{"d"})) {
    $debug = 1;
}
if (defined($options{"k"})) {
    $killmode = 1;
}
usage()
    if (! @ARGV);
$imageid = shift @ARGV;

#
# Untaint the argument.
#
if ($imageid =~ /^([-\@\w\+.]+)$/) {
    $imageid = $1;
}
else {
    die("Invalid image '$imageid' contains illegal characters.\n");
}

# Grab the filename to give to frisbee
my $filename = &get_filename($imageid);

#
# Make sure that the user has sufficient permissions.
#
if (!TBImageIDAccessCheck($UID, $imageid,
			  ($killmode ? TB_IMAGEID_DESTROY :
			   TB_IMAGEID_READINFO))) {
    tbdie("Not enough permission!");
}

if (!$killmode && !$ELABINELAB && ! -R $filename) {
    tbdie("You do not have permission to read the image file for".
	"imageid $imageid: $filename\n");
}

#
# Need to lock the tables here, so we can lock out anyone else from
# messing with the image (and so we can pick an address atomically). 
#
&lock_tables;

# Try to discover if some other process is handling this address
($address, $pid) = get_address($imageid);

if ($killmode) {
    #
    # Killmode. We do some sanity checking cause there is a small race 
    # inherent in trying to use the pid via the DB without locking (below).
    #
    # No point in leaving table locked; not going to modify it.
    #
    unlock_tables();

    # Nothing running.
    exit(0)
	if (!$address && !$pid);

    if (!$address && $pid) {
	#
	# This makes no sense. Just send email.
	#
	my $mesg = "Inconsistent DB state. PID ($pid) but no load address!";
	
	SENDMAIL($TBOPS,
		 "Frisbee Killer Failed!",
		 "Imageid: $imageid\n".
		 $mesg);
	
	tbdie($mesg);
    }
    if ($address && !$pid) {
	#
	# Okay, minor problem. It is possible we caught the launcher between
	# setting the address and setting the pid. Wait a moment, and then
	# try again. If still no pid, bail.
	#
	sleep(1);
	($address, $pid) = get_address($imageid);

	# Okay, situation resolved itself; other frisbeelauncher bailed.
	exit(0)
	    if (!$address && !$pid);

	#
	# Still inconsistent so bail.
	# 
	if ($address && !$pid) {
	    my $mesg = "Inconsistent DB state. Load address but no PID!";
	
	    SENDMAIL($TBOPS,
		     "Frisbee Killer Failed!",
		     "Imageid: $imageid\n".
		     $mesg);
	
	    tbdie($mesg);
	}
    }

    #
    # Okay, address and pid. We could clear the pid from the DB,
    # preventing another killer from thinking it is running, but not
    # much point since it is not likely to happen. If it turns out to
    # be a problem we can change the way this works.
    #
    unlock_tables();
    if (! kill('TERM', $pid)) {
	SENDMAIL($TBOPS,
		 "Frisbee Killer Failed!",
		 "Failed to stop frisbee daemon for $imageid\n".
		 "Could not kill(TERM) process $pid: $? $!");

	tbdie("Failed to stop frisbee daemon for $imageid!");
    }
    exit(0);
}

if ($address && &keepbusy($imageid)) {
        &unlock_tables;
	&debug("A server ($address) is already running for image $imageid\n");
	exit (0);
}

# This would be inconsistent.
if ($pid) {
    my $mesg = "Inconsistent DB state. No load address but PID ($pid) set!";
	
    SENDMAIL($TBOPS,
	     "Frisbee Startup Failed!",
	     "Imageid: $imageid\n".
	     $mesg);
	
    tbdie($mesg);
}

# Pick an address: Die if unsucessful, set address and unlock if sucessful
$address = &pick_address;
&debug("Picked address $address\n");

if (!$address) {
	&unlock_tables;
	die "Unable to find a free address to send on\n";
}

&set_address($imageid,$address);
&unlock_tables;

#
# When running inside an inner Emulab, try to get the image from the
# outer emulab when it does not exist locally. Of course, it could still
# be a bogus image.
#
if ($ELABINELAB && ! -e $filename) {
    $EUID = $UID;
    system("$FRISBEEIMAGE $imageid");
    if ($?) {
	tbdie("No such image file: $filename!");
    }
    $EUID = 0;
}

# Run in the background
if (my $childpid = TBBackGround($LOGFILE)) {
    #
    # Delay a moment, and they look for an exit status.
    # This is intended to catch startup problems.
    #
    # XXX TBBackGround should do this!
    #
    sleep(1);
    my $foo = waitpid($childpid, &WNOHANG);
    if ($foo) {
	&clear_address;
	tbdie("Error $? backgrounding frisbeelauncher!");
    }
    exit(0);
}
TBdbfork();

# Set up a signal handler that will clean up in case we get killed
$SIG{HUP} = $SIG{INT} = $SIG{TERM} = \&cleanup;

# Set our pid. This happens outside the lock which could lead to races,
# but that is unlikely. Look for it above though.
set_pid($imageid, $PID);

#
# Drop root permissions, if we have them
#
if ($EUID == 0) {
    $EUID = $UID;
}

my $args = "-i $BOSSNODE_IP";

#
# Select the appropriate bandwidth
#
if ($filename =~ /^$TB\/images/ && !$ELABINELAB) {
    $args .= " -W $STD_BW";
} else {
    $args .= " -W $USR_BW";
}

#
# Force multicast keepalives if necessary
#
if ($ELABINELAB) {
    $args .= " -K 15";
}

my $firsttry = 1;

# Now, we actually launch Frisbee
while (1) {
        #
        # Each time the server exits, test the busy bit to see if
	# it should keep going. This has to be done with tables locked
	# since another caller is going to bump it.
	#
	&lock_tables();
	if (! &testbusy($imageid)) {
		last;
	}
	&unlock_tables();
	
	if ($child_pid = fork()) {
		# Wait for child to exit
		waitpid($child_pid, 0);

		if ($?) {
		    #
		    # If this is the first time we are firing up this server
		    # and the port is already in use, try another port.
		    #
		    # If a server was running, died, and now we can no longer 
		    # get the port, something else might be wrong so just die
		    # in the usual way.
		    #
		    my $err = $?;
		    if ($firsttry && ($err >> 8) == EADDRINUSE()) {
			warn "Frisbeed bind failed for address $address, ".
			     "picking another address\n";
			&lock_tables();
			$address = &pick_address;
			if ($address) {
			    &set_address($imageid, $address);
			    &unlock_tables;
			    next;
			}
			&unlock_tables;
			warn "Unable to find a free address to send on\n";
		    }
		    SENDMAIL($TBOPS, "Frisbeed Failed!",
			     "Imageid: $imageid\n".
			     "Address: $address\n\n".
			     "Process $child_pid exited with value $err.\n".
			     "Please look at the syslog for frisbeed!\n\n".
			     "NOTE: Another frisbeed will not start!\n");
		    #
		    # Dump early. This will leave the address in
		    # in the DB, so that another one will not start
		    # until the matter is resolved by someone. 
		    #
		    set_pid($imageid, 0);
		    exit(1);
		} else {
		    $firsttry = 0;
		}
	}
	else {
		# Child branch
		# The database format for address is host:port - however,
		# we need to give them as seperate arguments to frisbeed.
		if ($address =~ /(.*):(.*)/) {
			my $addr = $1;
			my $port = $2;

			if (!exec("$FRISBEED $args -m $addr -p $port $filename")) {
				die("$$: Unable to exec $FRISBEED\n");
			}
		}
		die("$$: Bad address format: $address.\n");
	}
}

&clear_address;
&unlock_tables();
exit(0);

######################################################################
# Subroutines
######################################################################

# Only print if -d option was given. Also add $$ on the beginning of the
# string, to ease debugging
sub debug {
	if ($debug) { print "$$: ", @_ };
}

# Grab the address for the passed-in imageid
sub get_address {
	my ($imageid) = @_;

	my $sth =
	    DBQueryFatal("SELECT load_address,frisbee_pid ".
			 "FROM images WHERE imageid='$imageid'");

	my @row = $sth->fetchrow;
	if (!@row) {
		die "No such imageid: $imageid\n";
	}

	return ($row[0], $row[1]);
}

# Grab the filename for the passed-in imageid
sub get_filename {
	my ($imageid) = @_;
	my $image_query = "SELECT path FROM images WHERE " .
		"imageid='$imageid'";

	my $sth = DBQueryFatal($image_query);

	my @row = $sth->fetchrow;
	if (!@row) {
		die "No such imageid: $imageid\n";
	}

	return $row[0];
}


# Lock the tables used in this script - waits indefinitely until it
# succeeds
sub lock_tables {
	while (1) {
		&debug("Locking tables\n");
		my $sth = DBQuery("LOCK TABLES images WRITE, ".
				  "            emulab_indicies WRITE");
		if (!$sth) {
			print "DB Error locking tables. Waiting a bit ...\n";
			sleep(10);
		} else {
			last;
		}
	}
}

# Unlock the tables used in this script
sub unlock_tables {
	&debug("Unlocking tables\n");
	DBQueryFatal("UNLOCK TABLES"); 
}

# Pick out an address to use
sub pick_address {
    my $idx;
    my $baseaddr_query =
	DBQueryFatal("select idx from emulab_indicies ".
		     "where name='frisbee_mcastaddr'");
    if (! $baseaddr_query->num_rows) {
	$idx = unpack("N", inet_aton("${BASEADDR}.1"));
	
	DBQueryFatal("insert into emulab_indicies (name, idx) ".
		     "values ('frisbee_mcastaddr', $idx)");
    }
    else {
	($idx) = $baseaddr_query->fetchrow_array();
    }
    my ($a,$b,$c,$d) = unpack('C4', pack('N', $idx));
    # Bump to next address.
    if (++$d >= 255) {
	$d = 1;
	if (++$c >= 255) {
	    $c = 1;
	    if (++$b >= 255) {
		my $mesg = "No more multicast addresses!";
		
		SENDMAIL($TBOPS,
			 "FrisbeeLauncher Failed!",
			 "Imageid: $imageid\n".
			 $mesg);
	
		tbdie($mesg);
	    }
	}
    }
    my $address = "${a}.${b}.${c}.${d}";
    my $port    = $BASEPORT + ((($c << 8) | $d) & 0x7FFF);
    
    # Insert back into emulab_indicies for next time.
    $idx = unpack("N", inet_aton($address));

    DBQueryFatal("update emulab_indicies set idx=$idx ".
		 "where name='frisbee_mcastaddr'");

    return "${address}:${port}";
}

# Pass in an imageid, and an address
sub set_address {
	my ($imageid,$address) = @_;

	DBQueryFatal("UPDATE images SET load_address='$address',load_busy=1 " .
		     "WHERE imageid='$imageid'");
}

# Pass in an imageid and a pid.
sub set_pid {
	my ($imageid,$pid) = @_;

	DBQueryFatal("UPDATE images SET frisbee_pid=$pid " .
		     "WHERE imageid='$imageid'");
}

# Pass in an imageid and a pid.
sub clear_pid {
	my ($imageid) = @_;

	DBQueryFatal("UPDATE images SET frisbee_pid=0 " .
		     "WHERE imageid='$imageid'");
}

# Bump the busy indicator to keep the frisbeed going.
sub keepbusy($) {
	my ($imageid) = @_;

	DBQueryFatal("UPDATE images SET load_busy=GREATEST(load_busy,1) " .
		     "WHERE imageid='$imageid'");

	return 1;
}

# Test the busy indicator, and set to zero.
sub testbusy($) {
	my ($imageid) = @_;

	my $query_result =
	    DBQueryFatal("select load_busy from images ".
			 "WHERE imageid='$imageid'");

	my @row = $query_result->fetchrow;
	if (!@row) {
	        return 0;
	}
	
	if ($row[0]) {
	    DBQueryFatal("UPDATE images SET load_busy=0 ".
			 "WHERE imageid='$imageid'");
	}
	return $row[0];
}

# Kill off our child process, if started, and clear out registered address
# Also, die off
sub cleanup {
	print STDERR "$$: Killed, cleaning up\n";
	if ($child_pid) {
		kill 15, $child_pid;
	}
	&clear_address;
	exit(1);
}

# Clear out the address (and pid) registered to this process
sub clear_address {
	&debug("Clearing out registered load_address and pid\n");
	# Now, clear out the load_address we had set up
	DBQueryFatal("update images set ".
		     "load_address='',load_busy=0,frisbee_pid=0 ".
		     "where imageid='$imageid'");
}
