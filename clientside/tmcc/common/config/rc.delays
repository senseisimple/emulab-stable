#!/usr/bin/perl -w
#
# EMULAB-COPYRIGHT
# Copyright (c) 2004, 2005, 2009 University of Utah and the Flux Group.
# All rights reserved.
#
use English;
use Getopt::Std;

#
# An artifact is that all the delay stuff is handled in OS dependent
# scripts. This is just a wrapper. The delay agent is also started and
# stopped in the OS dependent stuff. Eventually clean this up.
#
# We ignore the -r option for now; delaysetup does not need it. 
#
sub usage()
{
    print "Usage: " .
	scriptname() . " [-j vnodeid] [-r rtabid] ".
	               "boot|shutdown|reconfig|reset\n";
    exit(1);
}
my $optlist = "j:r:";
my $action  = "boot";
my $vnodeid;

# Turn off line buffering on output
$| = 1;

# Drag in path stuff so we can find emulab stuff.
BEGIN { require "/etc/emulab/paths.pm"; import emulabpaths; }

# Only root.
if ($EUID != 0) {
    die("*** $0:\n".
	"    Must be root to run this script!\n");
}

# Script specific goo.
my $DELAYSETUP = "$BINDIR/delaysetup";
my $DELAYAGENT = "$BINDIR/rc/rc.delayagent";

#
# Load the OS independent support library. It will load the OS dependent
# library and initialize itself. 
# 
use libsetup;
use libtmcc;
use librc;

#
# Not all clients support this.
#
exit(0)
    if (MFS() || REMOTE() || JAILED() || INXENVM() || PLAB());

# Protos.
sub doboot();
sub doshutdown();
sub doreconfig();
sub docleanup();

# Parse command line.
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{'j'})) {
    $vnodeid = $options{'j'};
    libsetup_setvnodeid($vnodeid);
}
# Allow default above.
if (@ARGV) {
    $action = $ARGV[0];
}

# Execute the action.
SWITCH: for ($action) {
    /^boot$/i && do {
	doboot();
	last SWITCH;
    };
    /^shutdown$/i && do {
	doshutdown();
	last SWITCH;
    };
    /^reconfig$/i && do {
	doreconfig();
	last SWITCH;
    };
    /^reset$/i && do {
	docleanup();
	last SWITCH;
    };
    fatal("Invalid action: $action\n");
}
exit(0);

#
# Boot Action.
#
sub doboot()
{
    #
    # Just run the delaysetup script. It does it all.
    #
    my $optarg = (defined($vnodeid) ? "-j $vnodeid" : "");
    
    system("$DELAYSETUP -i $optarg");
    if ($?) {
	fatal("Could not install delays!");
    }
    #
    # Fire up the delayagent if we can (not done for linux yet).
    #
    if (-x $DELAYAGENT) {
	system("$DELAYAGENT $optarg start");
	if ($?) {
	    fatal("Could not start delay agent!");
	}
    }
}

#
# Shutdown Action.
#
sub doshutdown()
{
    #
    # Just run the delaysetup script. It does it all.
    #
    my $optarg = (defined($vnodeid) ? "-j $vnodeid" : "");

    #
    # Kill the delayagent if we can (not done for linux yet).
    #
    if (-x $DELAYAGENT) {
	system("$DELAYAGENT $optarg stop");
	if ($?) {
	    fatal("Could not stop delay agent!");
	}
    }
   
    system("$DELAYSETUP -u $optarg");
    if ($?) {
	fatal("Could not uninstall existing delays!");
    }
}

#
# Node Reconfig Action (without rebooting).
#
sub doreconfig()
{
    doshutdown();
    return doboot();
}

#
# Node cleanup action (node is reset to completely clean state).
#
sub docleanup()
{
    unlink TMDELAY, TMLINKDELAY, TMDELMAP;
}

