#!/usr/bin/perl -w
#
# EMULAB-COPYRIGHT
# Copyright (c) 2004, 2008 University of Utah and the Flux Group.
# All rights reserved.
#
use English;
use Getopt::Std;
use POSIX qw(setsid);
use Data::Dumper;
use strict;

sub usage()
{
    print "Usage: " .
	scriptname() . " [-j vnodeid] [-r rtabid] ".
	               "boot|shutdown|reconfig|reset\n";
    exit(1);
}
my $optlist = "j:r:";
my $action  = "boot";
my $vnodeid;
my $rtabid;		# Currently ignored.

# Turn off line buffering on output
$| = 1;

# Drag in path stuff so we can find emulab stuff.
BEGIN { require "/etc/emulab/paths.pm"; import emulabpaths; }

# Only root.
if ($EUID != 0) {
    die("*** $0:\n".
	"    Must be root to run this script!\n");
}

# Script specific goo.
my $VTUND       = "/usr/local/sbin/vtund";
my @vtundpids   = ();

#
# Load the OS independent support library. It will load the OS dependent
# library and initialize itself. 
# 
use libsetup;
use liblocsetup;
use libtmcc;
use librc;

#
# Not all clients support this.
#
exit(0)
    if (MFS() || PLAB() || JAILED());

# Now safe to do this since MFS does not have full perl install.
require Socket;
import Socket;

# Protos.
sub doboot();
sub doshutdown();
sub doreconfig();
sub docleanup();
sub DoVtun($);
sub DoGRE($);

# After libsetup include; tunnels run outside in fake vnode setup.
my $PIDFILE        = CONFDIR() . "/tunnel.pid";
my $VTUNDCONFIG    = CONFDIR() . "/vtund.conf";
my $LOGFILE        = $LOGDIR   . "/vtund.debug";

# Parse command line.
my %options;
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{'j'})) {
    $vnodeid = $options{'j'};
    libsetup_setvnodeid($vnodeid);
}
# Allow default above.
if (@ARGV) {
    $action = $ARGV[0];
}

# Execute the action.
SWITCH: for ($action) {
    /^boot$/i && do {
	doboot();
	last SWITCH;
    };
    /^shutdown$/i && do {
	doshutdown();
	last SWITCH;
    };
    /^reconfig$/i && do {
	doreconfig();
	last SWITCH;
    };
    /^reset$/i && do {
	docleanup();
	last SWITCH;
    };
    fatal("Invalid action: $action\n");
}
exit(0);

# Signal handler to initiate cleanup in parent and the children.
sub Pcleanup($)
{
    my ($signame) = @_;

    $SIG{TERM} = 'IGNORE';
    $SIG{INT}  = 'IGNORE';
    $SIG{HUP}  = 'IGNORE';

    foreach my $pid (@vtundpids) {
	system("kill $pid");
	waitpid($pid, 0);
    }

    unlink $PIDFILE;
    exit(0);
}

#
# Boot Action.
#
sub doboot()
{
    my $tunnels;

    print STDOUT "Checking Testbed tunnel configuration ... \n";

    if (gettunnelconfig(\$tunnels)) {
	fatal("Could not get tunnel configuration from libsetup!");
    }

    unlink $VTUNDCONFIG;
    return
	if (!keys(%{ $tunnels }));

    # XXX
    my $sysname = `uname -s`;
    chomp($sysname);
    if ($sysname eq "Linux") {
	system("insmod ip_gre");
    }
    else {
	system("kldload if_tap");
    }

    #
    # Write our pid to a file so we can be killed later.
    # 
    system("echo '$PID' > $PIDFILE") == 0
	or fatal("Could not create $PIDFILE!");
    
    $SIG{TERM} = \&Pcleanup;
    $SIG{INT}  = \&Pcleanup;
    $SIG{HUP}  = \&Pcleanup;

    #
    # First check for vtun.
    #
    DoVtun($tunnels);
    DoGRE($tunnels);

    # No reason to daemonize ...
    return
	if (!@vtundpids);

    #
    # Now daemonize and let the boot continue.
    #
    if (TBBackGround($LOGFILE)) {
	sleep(1);
	exit(0);
    }
    # Fully disconnect from bootup. 
    setsid();
    
    #
    # Just wait. We will die via the signal handler above.
    #
    while (1) {
	sleep(10000);
    }
    return;
}

sub DoVtun($)
{
    my ($tunnels) = @_;

    my ($pid, $eid, $vname) = check_nickname();

    if (! open(CONF, ">$VTUNDCONFIG")) {
	fatal("Could not open $VTUNDCONFIG: $!");
    }

    print(CONF
	  "options {\n".
	  "  ifconfig    /sbin/ifconfig;\n".
	  "  route       /sbin/route;\n".
	  "}\n".
	  "\n".
	  "default {\n".
	  "  persist     yes;\n".
	  "  stat        yes;\n".
	  "  keepalive   yes;\n".
	  "  type        ether;\n".
	  "}\n".
	  "\n");

    #
    # First construct the config file.
    # 
    foreach my $tunnel (values(%{ $tunnels })) {
	next
	    if ($tunnel->{"tunnel_style"} ne "vtun");
	
	my $name     = $tunnel->{"tunnel_lan"};
	my $isserver = $tunnel->{"tunnel_isserver"};
	my $destaddr = $tunnel->{"tunnel_dstip"};
	my $dstport  = $tunnel->{"tunnel_serverport"};
	my $password = $tunnel->{"tunnel_secretkey"};
	my $encrypt  = $tunnel->{"tunnel_encrypt"};
	my $compress = $tunnel->{"tunnel_compress"};
	my $inetip   = $tunnel->{"tunnel_ip"};
	my $mask     = $tunnel->{"tunnel_ipmask"};
	my $proto    = $tunnel->{"tunnel_proto"};

	$proto = "udp"
	    if (!defined($proto));
	$encrypt  = (defined($encrypt) && $encrypt ? "yes" : "no");
	$compress = (defined($compress) && $compress ? "yes" : "no");

	#
	# Sheesh, vtund fails if it sees "//" in a path. 
	#
	my $config = TMROUTECONFIG;
	$config =~ s/\/\//\//g;
	my $rtabopt= "";
	if (defined($rtabid)) {
	    $rtabopt = "    ifconfig \"%% rtabid $rtabid\";\n";
	}
	    
	print(CONF
	      "$name {\n".
	      "  password      $password;\n".
	      "  compress      $compress;\n".
	      "  encrypt       $encrypt;\n".
	      "  proto         $proto;\n".
	      "\n".
	      "  up {\n".
	      "    # Connection is Up\n".
	      $rtabopt .
	      "    ifconfig \"%% $inetip netmask $mask\";\n".
	      "    program " . $config . " \"$inetip up\" wait;\n".
	      "  };\n".
	      "  down {\n".
	      "    # Connection is Down\n".
	      "    ifconfig \"%% down\";\n".
	      "    program " . $config . " \"$inetip down\" wait;\n".
	      "  };\n".
	      "}\n\n");
    }
    close(CONF);

    #
    # Now fire off the vtund processes
    # 
    foreach my $tunnel (values(%{ $tunnels })) {
	next
	    if ($tunnel->{"tunnel_style"} ne "vtun");
	
	my $name     = $tunnel->{"tunnel_lan"};
	my $isserver = $tunnel->{"tunnel_isserver"};
	my $destaddr = $tunnel->{"tunnel_dstip"};
	my $dstport  = $tunnel->{"tunnel_serverport"};
	my $password = $tunnel->{"tunnel_secretkey"};
	my $encrypt  = $tunnel->{"tunnel_encrypt"};
	my $compress = $tunnel->{"tunnel_compress"};
	my $inetip   = $tunnel->{"tunnel_ip"};
	my $mask     = $tunnel->{"tunnel_ipmask"};
	my $proto    = $tunnel->{"tunnel_proto"};
	my $didserver= 0;

	my $log;
	my $cmd = "$VTUND -n -P $dstport -f $VTUNDCONFIG ";

	if ($isserver) {
	    if (!$didserver) {
		$cmd .= "-s";
		$log  = "$LOGDIR/vtund-${pid}-${eid}.debug";
		$didserver = 1;
	    }
	}
	else {
	    $cmd .= "$name $destaddr";
	    $log  = "$LOGDIR/vtun-${pid}-${eid}-${name}.debug";
	}

	#
	# Fork a child to run vtund. The parent saves up all the
	# pids so that we can react to a shutdown later. We do this cause
	# vtund is 3rd party and we do not have very good control
	# over its output, and we want to avoid a shell wrapper around it
	# by using a redirect. 
	#
	my $childpid = fork();
	if ($childpid) {
	    #
	    # Parent. Save pid and keep going.
	    #
	    push(@vtundpids, $childpid);
	    sleep(1);
	}
	else {
	    $SIG{TERM} = 'DEFAULT';
	    $SIG{INT}  = 'IGNORE';
	    $SIG{HUP}  = 'DEFAULT';

	    open(STDIN, "</dev/null") or
		fatal("opening /dev/null for STDIN: $!");

	    # Redirect stdout/stderr to log file.
	    open(STDOUT, ">>$log")
		or fatal("Could not redirect STDOUT");
	    open(STDERR, ">&STDOUT")
		or fatal("Could not redirect STDERR");

	    exec("$cmd");
	    die("*** $0:\n".
		"    Could not exec $cmd\n");
	}
    }
}

sub DoGRE($)
{
    my ($tunnels) = @_;

    my ($pid, $eid, $vname) = check_nickname();

    #
    # First construct the config file.
    # 
    foreach my $tunnel (values(%{ $tunnels })) {
	next
	    if ($tunnel->{"tunnel_style"} ne "gre");
	
	my $name     = $tunnel->{"tunnel_lan"};
	my $srchost  = $tunnel->{"tunnel_srcip"};
	my $dsthost  = $tunnel->{"tunnel_dstip"};
	my $inetip   = $tunnel->{"tunnel_ip"};
	my $peerip   = $tunnel->{"tunnel_peerip"};
	my $mask     = $tunnel->{"tunnel_ipmask"};
	my $unit     = $tunnel->{"tunnel_unit"};

	os_config_gre($name, $unit,
		      $inetip, $peerip, $mask, $srchost, $dsthost)
	    == 0 or fatal("Could not configure gre tunnel!");
    }
}

#
# Shutdown Action.
#
sub doshutdown()
{
    if (-e $PIDFILE) {
	system("kill `cat $PIDFILE`");
	sleep(1);
	# Wait for it to die.
	while (-e $PIDFILE) {
	    print "Waiting for $PIDFILE to die ...\n";
	    sleep(10);
	}
    }
}

#
# Node Reconfig Action (without rebooting).
#
sub doreconfig()
{
    # Shutdown tunnels before doing reconfig.
    doshutdown();
    return doboot();
}

#
# Node cleanup action (node is reset to completely clean state).
#
sub docleanup()
{
    unlink $VTUNDCONFIG, $PIDFILE;
}
