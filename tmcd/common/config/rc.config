#!/usr/bin/perl -w
#
# EMULAB-COPYRIGHT
# Copyright (c) 2004, 2005 University of Utah and the Flux Group.
# All rights reserved.
#
use English;
use Getopt::Std;

#
# This is the entrypoint to running the config scripts. It would be nice
# to just stick these in a directory and run them all, but order matters
# and I hate naming things so that the order comes up the way I want it!
# Anyway, this script fires off the rc scripts in the proper order.
#
# All of the scripts accept a -j argument since in some cases we run these
# on behalf of a vnode, and the script needs to tell the tmcc library the
# vnodeid for when it talks to tmcd. Otherwise, libtmcc knows when it is
# running *inside* a vnode and sets things up properly for talking to tmcd.
#
sub usage()
{
    print "Usage: " .
	scriptname() . " [-j vnodeid] boot|shutdown|reconfig|reset\n";
    exit(1);
}
my $optlist = "j:d";
my %options = ();
my $action  = "boot";
my $debug   = 0;
my $vnodeid;

# Turn off line buffering on output
$| = 1;

# Drag in path stuff so we can find emulab stuff.
BEGIN { require "/etc/emulab/paths.pm"; import emulabpaths; }

# Only root.
if ($EUID != 0) {
    die("*** $0:\n".
	"    Must be root to run this script!\n");
}

#
# Load the OS independent support library. It will load the OS dependent
# library and initialize itself. 
# 
use libsetup;
use libtmcc;
use librc;

# Protos.
sub doboot();
sub doshutdown();
sub doreconfig();
sub docleanup();

# Parse command line.
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{'j'})) {
    $vnodeid = $options{'j'};
    libsetup_setvnodeid($vnodeid);
}
if (defined($options{'d'})) {
    $debug++;
}
# Allow default above.
if (@ARGV) {
    $action = $ARGV[0];
}

#
# The master list of scripts. Add in the right order! To speed things up
# specify a shorter set for the MFS.
#
my @bootscripts;

if (MFS()) {
    @bootscripts = ("rc.misc", "rc.localize", "rc.mounts", "rc.accounts", 
		    "rc.hostnames", "rc.keys", "rc.tarfiles", "rc.rpms");
}
elsif (WINDOWS()) {
    @bootscripts = ("rc.misc", ##"rc.localize", 
		    "rc.keys", "rc.mounts",
		    "rc.accounts", "rc.topomap", 
		    "rc.route", "rc.ifconfig",
		    "rc.hostnames", "rc.syncserver", 
		    # rc.progagent is run by a separate service named ProgAgent.
		    # It's started by EmulabStartup after rc.bootsetup runs.
		    "rc.tarfiles", "rc.rpms");
}
else {
    @bootscripts = ("rc.firewall",
		    "rc.misc", "rc.localize", "rc.keys", "rc.mounts",
		    "rc.topomap", "rc.accounts",
		    "rc.route", "rc.tunnels", "rc.ifconfig", "rc.delays",
		    "rc.hostnames", "rc.trace", 
		    "rc.syncserver", "rc.trafgen",
		    "rc.tarfiles", "rc.rpms", "rc.progagent", "rc.linkagent",
		    "rc.tiptunnels", "rc.motelog", "rc.simulator");
}

# Execute the action.
SWITCH: for ($action) {
    /^boot$/i && do {
	doboot();
	last SWITCH;
    };
    /^shutdown$/i && do {
	doshutdown();
	last SWITCH;
    };
    /^reconfig$/i && do {
	doreconfig();
	last SWITCH;
    };
    /^reset$/i && do {
	docleanup();
	last SWITCH;
    };
    fatal("Invalid action: $action");
}
exit(0);

sub doaction($@)
{
    my ($what, @scripts) = @_;
    
    #
    # Run all the scripts. We run them all, not worrying about individual
    # failure. Right thing to do? Maybe stop on failure?
    #
    my $optarg = (defined($vnodeid) ? "-j $vnodeid" : "");

    foreach my $script (@scripts) {
	# No need to install all this cruft on the MFS or other small envs.
	next
	    if (!-x "$BINDIR/rc/$script");

	TBDebugTimeStamp("running $BINDIR/rc/$script");
	
	print "Running $BINDIR/rc/$script\n"
	    if ($debug);
	
	system("$BINDIR/rc/$script $optarg $what");
	if ($?) {
	    fatal("Failed running $BINDIR/rc/$script");
	}
    }
}

#
# Boot Action.
#
sub doboot()
{
    doaction("boot", @bootscripts);
}

#
# Shutdown Action.
#
sub doshutdown()
{
    doaction("shutdown", reverse(@bootscripts));
}

#
# Node Reconfig Action (without rebooting).
#
sub doreconfig()
{
    doshutdown();
    #
    # Must tell tmcc to reload its cache!
    # 
    tmccgetconfig();
    return doboot();
}

#
# Node cleanup action (node is reset to completely clean state).
#
sub docleanup()
{
    doaction("reset", @bootscripts);
}
